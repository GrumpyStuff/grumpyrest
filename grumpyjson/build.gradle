plugins {
	id 'java'
	id "com.github.ben-manes.versions" version "0.46.0" // support for dependency upgrade command
	id 'maven-publish'
}

group = 'name.martingeisse'
version = '0.1'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(20)
	}
	withJavadocJar()
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

sourceSets.main.resources.srcDir 'src/main/java'
sourceSets.test.resources.srcDir 'src/test/java'

sourcesJar {
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

test {
	useJUnitPlatform()
}

javadoc {
	source = sourceSets.main.allJava
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url "https://maven.google.com"
	}
}

dependencies {

	// common helper libraries
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'commons-io:commons-io:2.12.0'
	implementation 'org.apache.commons:commons-collections4:4.4'
	implementation 'com.google.guava:guava:31.1-jre'
	implementation 'commons-codec:commons-codec:1.15'

	// logging
	implementation 'org.slf4j:slf4j-api:2.0.7'

	// JSON
	implementation 'com.google.code.gson:gson:2.10.1'

	// testing
	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'

}
