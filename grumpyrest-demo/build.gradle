plugins {
	id 'java'
	id 'application'
	id "com.github.ben-manes.versions" version "0.46.0" // support for dependency upgrade command
	id 'maven-publish'
}

group = 'name.martingeisse'
version = '0.7'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

application {
	mainClass = 'name.martingeisse.grumpyrest_demo.Main'
}

sourceSets.main.resources.srcDir 'src/main/java'
sourceSets.test.resources.srcDir 'src/test/java'

sourcesJar {
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

test {
	useJUnitPlatform()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url "https://maven.google.com"
	}
}

dependencies {

	// other sub-porojects
	implementation project(':grumpyjson')
	implementation project(':grumpyrest')
	implementation project(':grumpyrest-jetty-launcher')

	// compile-time annotations for static code analysis
	implementation 'com.google.errorprone:error_prone_annotations:2.19.1'

	// common helper libraries
	implementation 'org.apache.commons:commons-lang3:3.12.0'

	// logging
	implementation 'org.slf4j:slf4j-api:2.0.7'
	implementation 'ch.qos.logback:logback-classic:1.4.7'

	// JSON
	implementation 'com.google.code.gson:gson:2.11.0'

	// testing
	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'

}
